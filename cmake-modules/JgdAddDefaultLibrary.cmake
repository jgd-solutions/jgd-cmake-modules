include_guard()

include(JgdParseArguments)
include(JgdValidateArguments)
include(JgdDefaultTargetProps)
include(JgdCanonicalStructure)
include(JgdFileNaming)
include(JgdGetDefaultLibraryTargetName)

#
# Creates a library with a name generated by JgdGetDefaultLibraryTargetName and
# the default target properties defined in JgdDefaultTargetProps. These include
# the target's output name, compile options, position independent code, and
# include directories.
#
# Additionally, an ALIAS library is created for use within the build tree
# (testing), regardless of if COMPONENT is provided. The alias will be
# <PROJECT_NAME>::<lib>, where 'lib' is either the generated library name or
# LIBRARY, if overridden.
#
# The libraries OUTPUT_NAME and PREFIX properties will only be set if the it is
# a STATIC or SHARED library, which will be <name>[-COMPONENT] and
# JGD_LIB_PREFIX, respectfully. This forms <JGD_LIB_PREFIX><name>[-COMPONENT] on
# disk.
#
# Arguments:
#
# COMPONENT: one-value arg; the component of the project that the library
# constitutes. A COMPONENT that matches the PROJECT_NAME will be ignored.
# Optional.
#
# LIBRARY: one-value arg; the override library name to create. Optional - if
# omitted, a library name will be generated.
#
# TYPE: one-value arg; the type of the library to generate. One of STATIC,
# SHARED, OBJECT, INTERFACE. Optional - if omitted, the generated library will
# be one of STATIC or SHARED, determined by CMake.
#
# SOURCES: multi value arg; the sources to create LIBRARY from.
#
function(jgd_add_default_library)
  jgd_parse_arguments(ONE_VALUE_KEYWORDS "COMPONENT;LIBRARY;TYPE"
                      MULTI_VALUE_KEYWORDS "SOURCES" ARGUMENTS "${ARGN}")
  jgd_validate_arguments(KEYWORDS "SOURCES")
  if(ARGS_COMPONENT AND NOT "${ARGS_COMPONENT}" STREQUAL "${PROJECT_NAME}")
    set(comp_arg COMPONENT ${ARGS_COMPONENT})
  endif()

  # Verify source naming
  foreach(source ${ARGS_SOURCES})
    set(regex "${JGD_HEADER_REGEX}|${JGD_SOURCE_REGEX}")
    string(REGEX MATCH "${regex}" matched "${source}")
    if(NOT matched)
      message(FATAL_ERROR "Provided source file, ${source}, does not match the"
                          "regex for library sources, ${regex}.")
    endif()
  endforeach()

  # Library name
  if(ARGS_LIBRARY)
    set(library "${ARGS_LIBRARY}")
  else()
    jgd_get_default_library_target_name(${comp_arg} OUT_VAR library)
  endif()

  # Override library type with TYPE, if provided and supported
  set(lib_type)
  if(ARGS_TYPE)
    set(supported_types STATIC SHARED OBJECT INTERFACE)
    list(FIND supported_types "${ARGS_TYPE}" supported)
    if(supported EQUAL -1)
      message(
        FATAL_ERROR
          "Unsupported type ${ARGS_TYPE}. ${CMAKE_CURRENT_FUNCTION} must be "
          "called with one of: ${supported_types}")
    endif()
    set(lib_type "${ARGS_TYPE}")
  endif()

  # Add the library with alias
  if("${lib_type}" STREQUAL "INTERFACE")
    add_library("${library}" INTERFACE)
  else()
    add_library("${library}" ${lib_type} "${ARGS_SOURCES}")
  endif()

  add_library(${PROJECT_NAME}::${library} ALIAS ${library})

  # Set default target properties

  # PIC option
  set_target_properties(
    ${library} PROPERTIES POSITION_INDEPENDENT_CODE
                          ${JGD_LIB_POSITION_INDEPENDENT_CODE})

  # change output name
  if(NOT lib_type OR "${lib_type}" MATCHES "STATIC|SHARED")
    jgd_default_library_output_name(LIBRARY ${library} OUT_VAR out_name)
    set_target_properties(${library} PROPERTIES OUTPUT_NAME ${out_name}
                                                PREFIX ${JGD_LIB_PREFIX})
  endif()

  # compile options
  set(include_access "PUBLIC")
  if("${lib_type}" STREQUAL "INTERFACE")
    set(include_access "INTERFACE")
  else()
    target_compile_options(${library} PRIVATE ${JGD_DEFAULT_COMPILE_OPTIONS})
  endif()

  # include directories
  jgd_default_include_dirs(TARGET ${library} BUILD_INTERFACE OUT_VAR
                           include_dirs)
  target_include_directories(${library} ${include_access} "${include_dirs}")
endfunction()
